buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
        classpath("net.saliman:gradle-cobertura-plugin:2.4.0")
    }
}

repositories {
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "https://oss.sonatype.org/" }
    maven { url "https://oss.sonatype.org/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://plugins.gradle.org/m2/"}
    mavenLocal()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'net.saliman.cobertura'

cobertura {
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    tasks.withType(Javadoc).all { enabled = false }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    // TODO - uncomment to upload
    //sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DCHQ SDK'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Contains DCHQ SDK.'
                url 'http://www.dchq.co'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Intesar'
                        name 'Intesar Mohammed'
                        email 'intesar@dchq.io'
                    }
                }
            }
        }
    }
}

group = dchqGroup
version = dchqVersion
jar {
    baseName = dchqName + '-' + 'SDK'
    version = dchqVersion + '-' + System.currentTimeMillis();
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework:spring-web")
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile("joda-time:joda-time")
    compile("io.dchq:DCHQ-dto-base:3.0-SNAPSHOT")
    compile("io.dchq:DCHQ-dto-one:3.0-SNAPSHOT") {
        exclude module: 'DTOResponseBase'
    }
    compile 'com.google.code.gson:gson:2.2.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    //compile("commons-codec:commons-codec:1.9")
    //compile("commons-collections:commons-collections:3.2.1")

    testCompile("junit:junit")


}
dependencyManagement {
    applyMavenExclusions false
}

